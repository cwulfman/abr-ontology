#+PROPERTY: header-args:sparql :url http://localhost:7200/repositories/abr :format text/csv

* Sample SPARQL Queries

** Forms that explicitly treat military/overseas voters
#+begin_src sparql :url http://localhost:7200/repositories/abr :format text/csv
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX oset: <http://oset.org/ontologies/abr/>

SELECT
  ?postalCode
  ?stateName
  (GROUP_CONCAT(DISTINCT ?reasonLabel; SEPARATOR=", ") AS ?reasons)
WHERE {

  # Concrete form
  ?form rdfs:subClassOf oset:ABRForm .
  FILTER (?form != oset:ABRForm)

  # Reason captured
  ?form rdfs:subClassOf [
    a owl:Restriction ;
    owl:onProperty oset:capturesInformationType ;
    owl:someValuesFrom ?reasonType
  ] .

  ?reasonType rdfs:subClassOf oset:MilitaryOrOverseasStatus .
  OPTIONAL { ?reasonType rdfs:label ?reasonLabel . }

  # State
  ?form rdfs:subClassOf [
    a owl:Restriction ;
    owl:onProperty oset:hasState ;
    owl:hasValue ?state
  ] .
  ?state oset:postalCode ?postalCode ;
         rdfs:label ?stateName .
}
GROUP BY ?postalCode ?stateName
ORDER BY ?postalCode
#+end_src

#+RESULTS:
| postalCode | stateName    | reasons                     |
|------------+--------------+-----------------------------|
| AK         | Alaska       | Military or Overseas Status |
| ND         | North Dakota | Military or Overseas Status |
| WI         | Wisconsin    | Military or Overseas Status |


** List all form classes and the documentation they require
#+begin_src sparql
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX oset: <http://oset.org/ontologies/abr/>

SELECT DISTINCT ?postalCode ?stateName
(GROUP_CONCAT(DISTINCT ?requirement; SEPARATOR=", ") AS ?requirements)
WHERE
{
      # Concrete form
  ?form rdfs:subClassOf oset:ABRForm .
  FILTER (?form != oset:ABRForm)
  
  ?form rdfs:subClassOf [
    a owl:Restriction ;
    owl:onProperty oset:requiresDocumentation ;
    owl:someValuesFrom ?docClass
    ] .
    
    OPTIONAL { ?docClass rdfs:label ?requirement }
    
   ?form rdfs:subClassOf [
    a owl:Restriction ;
    owl:onProperty oset:hasState ;
    owl:hasValue ?state
  ] .
    
    ?state a oset:State ;
         oset:postalCode ?postalCode ;
         rdfs:label ?stateName .

}
GROUP BY ?postalCode ?stateName
ORDER BY ?postalCode
#+end_src

#+RESULTS:
| postalCode | stateName      | requirements                                                                                                     |
|------------+----------------+------------------------------------------------------------------------------------------------------------------|
| AK         | Alaska         | Required Photo ID                                                                                                |
| AL         | Alabama        | Required Photo ID, Required Witness Signature, Required Agent or Assistant Certification                         |
| AR         | Arkansas       | Required Photo ID, Required Agent or Assistant Certification                                                     |
| FL         | Florida        | Required Agent or Assistant Certification                                                                        |
| GA         | Georgia        | Required Photo ID, Required Agent or Assistant Certification                                                     |
| IN         | Indiana        | Required Photo ID, Required Agent or Assistant Certification                                                     |
| KS         | Kansas         | Required Photo ID                                                                                                |
| LA         | Louisiana      | Required Photo ID, Required Witness Signature                                                                    |
| MD         | Maryland       | Required Agent or Assistant Certification                                                                        |
| MI         | Michigan       | Required Agent or Assistant Certification                                                                        |
| MO         | Missouri       | Required Photo ID                                                                                                |
| MT         | Montana        | Required Agent or Assistant Certification                                                                        |
| NC         | North Carolina | Required Agent or Assistant Certification                                                                        |
| ND         | North Dakota   | Required Witness Signature                                                                                       |
| NE         | Nebraska       | Required Photo ID, Required Agent or Assistant Certification                                                     |
| NH         | New Hampshire  | Required Photo ID, Required Witness Signature, Required Agent or Assistant Certification, Required Notarial Seal |
| NJ         | New Jersey     | Required Photo ID, Required Agent or Assistant Certification                                                     |
| NY         | New York       | Required Witness Signature, Required Agent or Assistant Certification                                            |
| OH         | Ohio           | Required Photo ID                                                                                                |
| OK         | Oklahoma       | Required Agent or Assistant Certification                                                                        |
| PA         | Pennsylvania   | Required Photo ID, Required Witness Signature                                                                    |
| SD         | South Dakota   | Required Photo ID, Required Notarial Seal                                                                        |
| TX         | Texas          | Required Witness Signature, Required Agent or Assistant Certification                                            |
| WI         | Wisconsin      | Required Photo ID, Required Witness Signature, Required Agent or Assistant Certification                         |

** Forms that require a witness signature
#+begin_src sparql
  PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
  PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
  PREFIX owl:  <http://www.w3.org/2002/07/owl#>
  PREFIX oset: <http://oset.org/ontologies/abr/>

  SELECT DISTINCT ?postalCode ?stateName ?form
  WHERE {
    # Only concrete ABR form classes
    ?form a owl:Class ;
          rdfs:subClassOf oset:ABRForm .
    FILTER (?form != oset:ABRForm)

    # Form has a restriction requiring witness signature
    ?form rdfs:subClassOf [
      a owl:Restriction ;
      owl:onProperty oset:requiresDocumentation ;
      owl:someValuesFrom ?docClass
    ] .

    # The documentation is (or is a subclass of) RequiredWitnessSignature
    ?docClass rdfs:subClassOf* oset:RequiredWitnessSignature .

    # Link to state via hasState
    ?form rdfs:subClassOf [
      a owl:Restriction ;
      owl:onProperty oset:hasState ;
      owl:hasValue ?state
    ] .

    ?state a oset:State ;
           oset:postalCode ?postalCode ;
           rdfs:label ?stateName .
  }
  ORDER BY ?postalCode
#+end_src

#+RESULTS:
| postalCode | stateName     | form                                   |
|------------+---------------+----------------------------------------|
| AL         | Alabama       | http://oset.org/ontologies/abr/AL_Form |
| LA         | Louisiana     | http://oset.org/ontologies/abr/LA_Form |
| ND         | North Dakota  | http://oset.org/ontologies/abr/ND_Form |
| NH         | New Hampshire | http://oset.org/ontologies/abr/NH_Form |
| NY         | New York      | http://oset.org/ontologies/abr/NY_Form |
| PA         | Pennsylvania  | http://oset.org/ontologies/abr/PA_Form |
| TX         | Texas         | http://oset.org/ontologies/abr/TX_Form |
| WI         | Wisconsin     | http://oset.org/ontologies/abr/WI_Form |


** States that require an excuse
#+begin_src sparql
  PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
  PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
  PREFIX owl:  <http://www.w3.org/2002/07/owl#>
  PREFIX oset: <http://oset.org/ontologies/abr/>

  SELECT
    ?postalCode
    ?stateName
    (GROUP_CONCAT(DISTINCT ?reasonLabel; SEPARATOR=", ") AS ?reasons)
  WHERE {

    # Concrete form
    ?form rdfs:subClassOf oset:ABRForm .
    FILTER (?form != oset:ABRForm)

    # Reason captured
    ?form rdfs:subClassOf [
      a owl:Restriction ;
      owl:onProperty oset:capturesInformationType ;
      owl:someValuesFrom ?reasonType
    ] .

    ?reasonType rdfs:subClassOf oset:ReasonForAbsenteeRequest .
    OPTIONAL { ?reasonType rdfs:label ?reasonLabel . }

    # State
    ?form rdfs:subClassOf [
      a owl:Restriction ;
      owl:onProperty oset:hasState ;
      owl:hasValue ?state
    ] .
    ?state oset:postalCode ?postalCode ;
           rdfs:label ?stateName .
  }
  GROUP BY ?postalCode ?stateName
  ORDER BY ?postalCode
#+end_src

#+RESULTS:
| postalCode | stateName   | reasons                                                                                                                                                                 |
|------------+-------------+-------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
| CT         | Connecticut | Religious Conflict Reason                                                                                                                                               |
| MO         | Missouri    | Illness or Disability Reason, Incarceration Reason, Nursing Home or Long-Term Care Reason, Out-of-County Travel Reason, Religious Conflict Reason, Work Conflict Reason |
| OH         | Ohio        | Work Conflict Reason                                                                                                                                                    |
| OK         | Oklahoma    | Nursing Home or Long-Term Care Reason                                                                                                                                   |
| WI         | Wisconsin   | Illness or Disability Reason                                                                                                                                            |
